import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
/**
 * for Unit Test. [template.annotation.Mockable]
 */
apply plugin: 'kotlin-allopen'

ext {
    getBuildTime = {
        def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
        df.setTimeZone(TimeZone.getTimeZone("UTC"))
        return df.format(new Date())
    }
}

android {
    def android = rootProject.extensions.ext.android
    def app = rootProject.extensions.ext.app

    compileSdkVersion android.compileSdk
    buildToolsVersion android.buildTools

    defaultConfig {
        applicationId android.applicationId
        versionName = app.versionName
        versionCode = app.versionCode

        minSdkVersion android.minSdk
        targetSdkVersion android.targetSdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BUILD_TIME", "\"${getBuildTime()}\""

        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField "String", "END_POINT", '"https://api.coindesk.com/v1/bpi/"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
        release {
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "END_POINT", '"https://api.coindesk.com/v1/bpi/"'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

androidExtensions {
    experimental = true
}

/**
 * for Unit Test. [template.annotation.Mockable]
 */
allOpen {
    annotation('template.annotation.Mockable')
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    def version = rootProject.extensions.ext.version

    testImplementation "junit:junit:$version.junit"
    testImplementation "org.mockito:mockito-core:2.+"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$version.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // android support library
    implementation "com.android.support:appcompat-v7:$version.support_v7"
    implementation "com.android.support:recyclerview-v7:$version.support_v7"
    implementation "com.android.support:cardview-v7:$version.support_v7"
    implementation "com.android.support:design:$version.support_v7"
    implementation "com.android.support:customtabs:$version.support_v7"
    implementation "com.android.support.constraint:constraint-layout:$version.constraintlayout"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version.kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$version.kotlin"

    // coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version.coroutines"

    // conductor & nucleus
    implementation "com.bluelinelabs:conductor:$version.conductor"
    implementation "com.bluelinelabs:conductor-support:$version.conductor"
    implementation "info.android15.nucleus5:nucleus:$version.nucleus"

    // Glide
    implementation "com.github.bumptech.glide:glide:$version.glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$version.glide"
    kapt "com.github.bumptech.glide:compiler:$version.glide"

    // Transformations
    implementation 'jp.wasabeef:glide-transformations:3.1.1'

    // dagger2
    kapt "com.google.dagger:dagger-compiler:$version.dagger"
    implementation "com.google.dagger:dagger:$version.dagger"

    // JSON
    implementation "com.google.code.gson:gson:$version.gson"
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'

    // retrofit
    implementation "com.squareup.okhttp3:okhttp:$version.okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$version.okhttp_log"
    implementation "com.squareup.retrofit2:retrofit:$version.retrofit2"
    implementation "com.squareup.retrofit2:converter-gson:$version.retrofit2"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$version.retrofit2"

    // rxjava
    implementation "io.reactivex.rxjava2:rxjava:$version.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$version.rxandroid"

    // RxPreferences
    implementation "com.f2prateek.rx.preferences2:rx-preferences:$version.rxpreference"

    // RxBinding
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$version.rxbinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$version.rxbinding"

    // RxNetwork
    implementation 'com.github.pwittchen:reactivenetwork-rx2:3.0.0'

    // log
    implementation "com.jakewharton.timber:timber:$version.timber"

    // database
    implementation "com.pushtorefresh.storio3:sqlite:$version.storio"

    // JavaScript engine
    implementation "com.squareup.duktape:duktape-android:$version.duktape"

    // flex-adapter
    implementation 'eu.davidea:flexible-adapter:5.0.5'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0-b5'

    // Saves and restores View states in ViewPagers
    implementation 'com.nightlynexus.viewstatepageradapter:viewstatepageradapter:1.0.4'

    // HTML parser
    implementation 'org.jsoup:jsoup:1.10.2'

    // material design dimens
    implementation 'com.dmitrymalkovich.android:material-design-dimens:1.4'

    // file picker
    implementation 'com.nononsenseapps:filepicker:4.1.0'

    // Tag
    implementation 'me.gujun.android.taggroup:library:1.4@aar'

    // Library to create preference screens using Conductor controllers instead of fragments.
    implementation 'com.github.inorichi:conductor-support-preference:27.0.2'

    // material-dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    // subsampling-scale-image
    implementation 'com.github.inorichi:subsampling-scale-image-view:81b9d68'

    // toggle system ui
    implementation 'me.zhanghai.android.systemuihelper:library:1.0.0'

    // Disk
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation 'com.github.inorichi:unifile:e9ee588'

    // Sort
    implementation 'com.github.gpanther:java-nat-sort:natural-comparator-1.1'

    implementation 'com.github.inorichi:junrar-android:634c1f5'
}